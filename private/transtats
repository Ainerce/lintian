#!/usr/bin/perl -w

# transtats - tag classification transition statistics
#
# This script displays statistics and data related to the experimental tag
# classification based on Severity/Certainty headers, as well as its
# transition from the Type based classification.
#
# The verbose options (-v, -vv, -vvv) can be used to display a detailed list
# of which tags are assigned to each category.

use strict;
use warnings;

my $LINTIAN_ROOT = $ENV{'LINTIAN_ROOT'};
unshift @INC, "$LINTIAN_ROOT/lib";

require Read_pkglists;

my @severities = qw(serious important normal minor wishlist);
my @certainties = qw(certain possible wild-guess);
my @types = qw(error warning info);

my %stats;
my $num_tags = 0;
my $num_done = 0;
my $per_done = 0;

my $verbose = $ARGV[0] ? ($ARGV[0] =~ s/v/v/g) : 0;

opendir(CHECKDIR, "$LINTIAN_ROOT/checks")
    or fail("cannot read directory $LINTIAN_ROOT/checks");

for my $check (readdir CHECKDIR) {
    next unless $check =~ /\.desc$/;

    my @tags = read_dpkg_control("$LINTIAN_ROOT/checks/$check");
    shift(@tags);

    foreach my $tag (@tags) {
        my $name = $tag->{tag};
        my $type = $tag->{type};
        my $severity = $tag->{severity};
        my $certainty = $tag->{certainty};
        my $done = $severity and $certainty ? 1 : 0;

        $severity = 'unclassified' if not $severity;
        $certainty = 'unclassified' if not $certainty;

        push(@{$stats{severity}{$severity}}, $name);
        push(@{$stats{certainty}{$certainty}}, $name);
        push(@{$stats{both}{$severity}{$certainty}}, $name);
        push(@{$stats{type}{severity}{$type}{$severity}}, $name);
        push(@{$stats{type}{both}{$type}{$severity}{$certainty}}, $name);

        $num_tags++;
        $num_done++ if $done;
    }
}

closedir(CHECKDIR);

$per_done = sprintf("%.2f", ($num_done/$num_tags)*100);
print "Number of classified tags\n";
print "  $num_done/$num_tags ($per_done%)\n";

print "\nSeverity\n";
foreach my $s (@severities) {
    my $tags = $stats{severity}{$s};
    print "  $s: ". @{$tags} ."\n";
    print "    ". join("\n    ", @{$tags}) ."\n" if $verbose >= 3;
}

print "\nCertainty\n";
foreach my $c (@certainties) {
    my $tags = $stats{certainty}{$c};
    print "  $c: ". @{$tags} ."\n";
    print "    ". join("\n    ", @{$tags}) ."\n" if $verbose >= 3;
}

print "\nSeverity/Certainty\n";
foreach my $s (@severities) {
    foreach my $c (@certainties) {
        if (my $tags = $stats{both}{$s}{$c}) {
            print "  $s/$c: ". @{$tags} ."\n";
            print "    ". join("\n    ", @{$tags}) ."\n" if $verbose >= 2;
        }
    }
}

foreach my $t (@types) {
    print "\nType $t Severity\n";
    foreach my $s (@severities) {
        if (my $tags = $stats{type}{severity}{$t}{$s}) {
            print "  $s: ". @{$tags} ."\n";
            print "    ". join("\n    ", @{$tags}) ."\n" if $verbose >= 2;
        }
    }
}

foreach my $t (@types) {
    print "\nType $t Severity/Certainty\n";
    foreach my $s (@severities) {
        foreach my $c (@certainties) {
            if (my $tags = $stats{type}{both}{$t}{$s}{$c}) {
                print "  $s/$c: ". @{$tags} ."\n";
                print "    ". join("\n    ", @{$tags}) ."\n" if $verbose >= 1;
            }
        }
    }
}

# vim: sw=4 sts=4 ts=4 et sr
