#!/usr/bin/perl

use strict;
use warnings;

use POSIX qw(strftime);

my $datadir = shift;
my $man = shift // '/usr/share/man/man8/ld.so.8.gz';
my %caps;

die "Usage: $0 path/to/lintian/data.\n" unless $datadir;

open my $manpage, '-|', 'zcat', $man or die "zcat $man: $!";
while (<$manpage>) {
    next unless /^\.SH HARDWARE CAPABILITIES/;
    last;
}
while (<$manpage>) {
    next unless /^\.B/;
    last;
}
while (<$manpage>) {
    last if /^\.SH /;
    next if /^\./;
    chomp;
    $caps{$_} = 1 foreach split /,\s*/;
}
close $manpage or die "zcat: $!";

my $path = "$datadir/shared-libs/hwcap-dirs";
my $date = strftime '%Y-%m-%d', gmtime;
open my $fp, '>', $path or die "Opening $path: $!";
print $fp <<EOF ;
# List of all known hwcap.
#
# Last updated: $date
# Generated by $0

EOF
foreach (sort keys %caps) {
    print $fp "$_\n";
}
close $fp or die "Closing $path: $!";

# Local Variables:
# indent-tabs-mode: nil
# cperl-indent-level: 4
# End:
# vim: syntax=perl sw=4 sts=4 sr et
