#!/usr/bin/perl -w
# diffstat -- lintian collection script for source packages

# Copyright (C) 1998 Richard Braakman
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program.  If not, you can find it on the World Wide
# Web at http://www.gnu.org/copyleft/gpl.html, or write to the Free
# Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston,
# MA 02110-1301, USA.

# This could be written more easily in shell script, but I'm trying
# to keep everything as perl to cut down on the number of processes
# that need to be started in a lintian scan.  Eventually all the
# perl code will be perl modules, so only one perl interpreter
# need be started.

use strict;
use warnings;

use lib "$ENV{'LINTIAN_ROOT'}/lib";
use Util;

($#ARGV == 2) or fail 'syntax: diffstat <pkg> <type> <dir>';
my ($pkg, undef, $dir) = @ARGV;
my $data;
my $ver;

-f "$dir/dsc" or fail 'diffstat invoked with wrong dir argument';

$data = get_dsc_info ("$dir/dsc");
$ver = $data->{'version'};

unlink "$dir/debian-patch" or fail "cannot unlink debian-patch: $!"
    if -e "$dir/debian-patch" or -l "$dir/debian-patch";

$ver =~ s/^\d://; #Remove epoch for this

my $diff_file = "$dir/${pkg}_${ver}.diff.gz";
exit 0 unless (-f $diff_file);

gunzip_file($diff_file, "$dir/debian-patch");

open STAT, '>', "$dir/diffstat" or fail "cannot open scripts output file: $!";
# diffstat is noisy on stderr if its stdout is not a tty.
# Shut it up by redirecting stderr to /dev/null.
open STDERR, '>', '/dev/null';
open DIFF, '-|', 'diffstat',  '-p1', "$dir/debian-patch"
  or fail("cannot open pipe to diffstat on debian-patch: $!");
# Copy all except last line to the STAT file
my $previous;
while (<DIFF>) {
    print STAT $previous if $previous;
    $previous = $_;
}
close DIFF or fail("cannot close pipe to diffstat on debian-patch: $!");
close STAT or fail("error writing diffstat file: $!");

# Local Variables:
# indent-tabs-mode: nil
# cperl-indent-level: 4
# End:
# vim: syntax=perl sw=4 sts=4 sr et
